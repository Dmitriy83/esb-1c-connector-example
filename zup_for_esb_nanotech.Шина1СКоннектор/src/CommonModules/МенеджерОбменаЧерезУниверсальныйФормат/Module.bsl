#Область СлужебныеПроцедурыИФункции

&После("ПередКонвертацией")
Процедура шинаПередКонвертацией(КомпонентыОбмена)
	КомпонентыОбмена.ПараметрыКонвертации.ОтправлятьСотрудников = Истина;
КонецПроцедуры

&ИзменениеИКонтроль("ДобавитьПКО_Справочник_Сотрудники_Отправка")
Процедура шинаДобавитьПКО_Справочник_Сотрудники_Отправка1(ПравилаКонвертации)
	ПравилоКонвертации = ОбменДаннымиXDTOСервер.ИнициализироватьПравилоКонвертацииОбъекта(ПравилаКонвертации);
	ПравилоКонвертации.ИмяПКО            = "Справочник_Сотрудники_Отправка";
	ПравилоКонвертации.ОбъектДанных      = Метаданные.Справочники.Сотрудники;
	ПравилоКонвертации.ОбъектФормата     = "Справочник.Сотрудники";
	СвойстваШапки = ПравилоКонвертации.Свойства;
	ДобавитьПКС(СвойстваШапки, "ВАрхиве",               "Архив");
	ДобавитьПКС(СвойстваШапки, "ГоловнаяОрганизация",   "ГоловнаяОрганизация", , "Справочник_Организации_Отправка");
	ДобавитьПКС(СвойстваШапки, "ГоловнойСотрудник",     "ГоловнойСотрудник", , "Справочник_Сотрудники_Отправка");
	ДобавитьПКС(СвойстваШапки, "Код",                   "ТабельныйНомер");
	ДобавитьПКС(СвойстваШапки, "Наименование",          "Наименование");
	ДобавитьПКС(СвойстваШапки, "УточнениеНаименования", "УточнениеНаименования");
	ДобавитьПКС(СвойстваШапки, "ФизическоеЛицо",        "ФизическоеЛицо", , "Справочник_ФизическиеЛица_Отправка");	
	#Вставка
	ПравилоКонвертации.ПриОтправкеДанных = "ПКО_Справочник_Сотрудники_Отправка_ПриОтправкеДанных";
	#КонецВставки
КонецПроцедуры  

&ИзменениеИКонтроль("ДобавитьПКО_Справочник_СтруктураПредприятия_Отправка")
Процедура шинаДобавитьПКО_Справочник_СтруктураПредприятия_Отправка(ПравилаКонвертации)
	ПравилоКонвертации = ОбменДаннымиXDTOСервер.ИнициализироватьПравилоКонвертацииОбъекта(ПравилаКонвертации);
	ПравилоКонвертации.ИмяПКО            = "Справочник_СтруктураПредприятия_Отправка";
	ПравилоКонвертации.ОбъектДанных      = Метаданные.Справочники.СтруктураПредприятия;
	ПравилоКонвертации.ОбъектФормата     = "Справочник.Подразделения";
	СвойстваШапки = ПравилоКонвертации.Свойства;
	ДобавитьПКС(СвойстваШапки, "Наименование", "Наименование");
	ДобавитьПКС(СвойстваШапки, "Родитель",     "ВышестоящееПодразделение", , "Справочник_СтруктураПредприятия_Отправка");
	#Вставка
	ПравилоКонвертации.ПриОтправкеДанных = "ПКО_Справочник_СтруктураПредприятия_Отправка_ПриОтправкеДанных";
	#КонецВставки
КонецПроцедуры

&После("ВыполнитьПроцедуруМодуляМенеджера")
Процедура шинаВыполнитьПроцедуруМодуляМенеджера(ИмяПроцедуры, Параметры)
	Если ИмяПроцедуры = "ПКО_Справочник_Сотрудники_Отправка_ПриОтправкеДанных" Тогда 
		ПКО_Справочник_Сотрудники_Отправка_ПриОтправкеДанных(Параметры.ДанныеИБ, Параметры.ДанныеXDTO, Параметры.КомпонентыОбмена, Параметры.СтекВыгрузки);
	ИначеЕсли ИмяПроцедуры = "ПКО_Справочник_СтруктураПредприятия_Отправка_ПриОтправкеДанных" Тогда 
		ПКО_Справочник_СтруктураПредприятия_Отправка_ПриОтправкеДанных(Параметры.ДанныеИБ, Параметры.ДанныеXDTO, Параметры.КомпонентыОбмена, Параметры.СтекВыгрузки);
	КонецЕсли;
КонецПроцедуры

//@skip-check extension-method-prefix
Процедура ПКО_Справочник_Сотрудники_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудника(Истина, ДанныеИБ, "Должность, Подразделение, МестоВСтруктуреПредприятия", ТекущаяДатаСеанса());
	Если КадровыеДанные <> Неопределено Тогда  
		Если ДанныеXDTO.Свойство("AdditionalInfo") И ТипЗнч(ДанныеXDTO.AdditionalInfo) = Тип("Структура") Тогда
			ДополнительныеДанные = ДанныеXDTO.AdditionalInfo;
		Иначе
			ДополнительныеДанные = Новый Структура;
		КонецЕсли; 		
		ДополнительныеДанные.Вставить("ДолжностьИд", 			XMLСтрока(КадровыеДанные.Должность));
		ДополнительныеДанные.Вставить("ПодразделениеИд", 		XMLСтрока(КадровыеДанные.Подразделение));
	    ДополнительныеДанные.Вставить("ПодразделениеРеглИд", 	XMLСтрока(КадровыеДанные.МестоВСтруктуреПредприятия));
		ДанныеXDTO.Вставить("AdditionalInfo", ДополнительныеДанные); 
	КонецЕсли;
		
КонецПроцедуры

//@skip-check extension-method-prefix
Процедура ПКО_Справочник_СтруктураПредприятия_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	
	Руководители = РуководителиПодразделений.ДанныеРуководителей(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеИБ));
	ДанныеРуководителя = Руководители[ДанныеИБ];
	Если ДанныеРуководителя <> Неопределено Тогда	
		Если ДанныеXDTO.Свойство("AdditionalInfo") И ТипЗнч(ДанныеXDTO.AdditionalInfo) = Тип("Структура") Тогда
			ДополнительныеДанные = ДанныеXDTO.AdditionalInfo;
		Иначе
			ДополнительныеДанные = Новый Структура;
		КонецЕсли; 		
		ДополнительныеДанные.Вставить("РуководительСотрудникИд", 	XMLСтрока(ДанныеРуководителя.Руководитель));
		ДополнительныеДанные.Вставить("РуководительФизлицоИд", 		XMLСтрока(ДанныеРуководителя.ФизическоеЛицо));
		ДанныеXDTO.Вставить("AdditionalInfo", ДополнительныеДанные); 	
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти