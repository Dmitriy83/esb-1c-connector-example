#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выгрузить(Команда)
	
	XMLПредставлениеОбъекта = ВыгрузитьНаСервере(Объект.СсылкаДляВыгрузки);

КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ЗагрузитьНаСервере(XMLПредставлениеОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//@skip-check bsl-legacy-check-string-literal		
&НаСервереБезКонтекста
Функция ВыгрузитьНаСервере(СсылкаДляВыгрузки)
	
	КомпонентыОбмена = ОбменДаннымиXDTOСервер.ИнициализироватьКомпонентыОбмена("Отправка");	
	КомпонентыОбмена.ВедениеПротоколаДанных.ВыводВПротоколИнформационныхСообщений = Ложь;
	КомпонентыОбмена.КлючСообщенияЖурналаРегистрации = ОбменДаннымиСервер.СобытиеЖурналаРегистрацииОбменДанными();
	КомпонентыОбмена.ЭтоОбменЧерезПланОбмена = Ложь;
	КомпонентыОбмена.ВерсияФорматаОбмена = "1.16";
	КомпонентыОбмена.XMLСхема = "http://v8.1c.ru/edi/edi_stnd/EnterpriseData/" + КомпонентыОбмена.ВерсияФорматаОбмена;
	Если Метаданные.Имя = "ДокументооборотКОРП" Тогда
		КомпонентыОбмена.МенеджерОбмена = ОбщегоНазначения.ОбщийМодуль("МенеджерОбменаЧерезУниверсальныйФормат_1_8");
	Иначе
		КомпонентыОбмена.МенеджерОбмена = ОбщегоНазначения.ОбщийМодуль("МенеджерОбменаЧерезУниверсальныйФормат");
	КонецЕсли;
	ОбменДаннымиXDTOСервер.ИнициализироватьТаблицыПравилОбмена(КомпонентыОбмена);
	ОбменДаннымиXDTOСервер.ОткрытьФайлВыгрузки(КомпонентыОбмена);  	
	Если КомпонентыОбмена.ФлагОшибки Тогда
		Возврат КомпонентыОбмена.СтрокаСообщенияОбОшибке;
	КонецЕсли;
	
	Попытка
		КомпонентыОбмена.МенеджерОбмена.ПередКонвертацией(КомпонентыОбмена);
	Исключение
		Возврат КомпонентыОбмена.СтрокаСообщенияОбОшибке;
	КонецПопытки;
	
	ОбменДаннымиXDTOСервер.ВыгрузкаОбъектаВыборки(КомпонентыОбмена, СсылкаДляВыгрузки);
	
	Попытка
		КомпонентыОбмена.МенеджерОбмена.ПослеКонвертации(КомпонентыОбмена);
	Исключение
		Возврат КомпонентыОбмена.СтрокаСообщенияОбОшибке;
	КонецПопытки;
	
	КомпонентыОбмена.ФайлОбмена.ЗаписатьКонецЭлемента(); // Body
	КомпонентыОбмена.ФайлОбмена.ЗаписатьКонецЭлемента(); // Message
	
	Если КомпонентыОбмена.СостояниеОбменаДанными.РезультатВыполненияОбмена = Неопределено Тогда
		КомпонентыОбмена.СостояниеОбменаДанными.РезультатВыполненияОбмена = Перечисления.РезультатыВыполненияОбмена.Выполнено;
	КонецЕсли;
	
	Возврат КомпонентыОбмена.ФайлОбмена.Закрыть();

КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьНаСервере(Знач XMLПредставлениеОбъекта)
	
	Обработки.ВыгрузкаЗагрузкаEnterpriseData.Создать().ЗагрузкаСообщения(Новый Структура("ТекстXML", XMLПредставлениеОбъекта), Неопределено); 
	
КонецПроцедуры

#КонецОбласти